prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
prefix schema:<http://schema.org/>
ex:NXQ_00253
  sh:prefixes _:sparql_examples_prefixes, _:nextprot_sparql_examples_prefixes ;
  rdfs:comment '''Human pathways in which at least one protein is mitochondrial GOLD''' ;
 schema:keyword "pathway";
 schema:keyword "federated query";
 schema:keyword "tutorial";
 schema:keyword "subcellular location";
 schema:keyword "quality";
  sh:select '''select ?pathwayname (group_concat(distinct ?gen ; SEPARATOR = ',') as ?gene) where {
 service <https://sparql.wikipathways.org/sparql> {

 { ?geneProduct a wp:GeneProduct . }
 union
 { ?geneProduct a wp:Protein . }
 ?geneProduct rdfs:label ?genraw .
 bind (concat( \"\"^^xsd:string, ?genraw) as ?gen).
 filter(!regex(?gen,\"[ a-z-]\")). # ensures official gene names for subsequent neXtprot matching
 ?geneProduct dcterms:isPartOf ?pathway .
 ?pathway a wp:Pathway .
 ?pathway wp:organism ?organism .
 filter(contains(str(?organism),\"9606\"))
 ?pathway dc:title ?pathwayname .
 }

 ?entry a :Entry .
 ?entry :gene / :recommendedName / rdfs:label ?gen .
 ?entry :isoform / :cellularComponent ?loc .
 values ?mitoloc {nextprot_cv:SL-0173 nextprot_cv:GO_0005739 } # SL and GO values for mitochondrion
 ?loc :term / :childOf ?mitoloc. # mitochondrion
 ?loc :evidence / :quality :GOLD .
 filter not exists {?loc :negativeEvidence ?negev} # No negative localization evidence
}
group by ?pathwayname
order by ?pathwayname'''
.
