prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
prefix schema:<http://schema.org/>
ex:NXQ_00271
  sh:prefixes _:sparql_examples_prefixes, _:nextprot_sparql_examples_prefixes ;
  rdfs:comment '''Polymorphisms located on ACE2 and TMPRSS2 and affecting proteinsâ€™ activity, structure, PTM...''' ;
 schema:keyword "variant";
 schema:keyword "tutorial";
  sh:select '''select distinct (str(?gn) as ?gene) ?pos (str(?snpac) as ?varid) (str(?orgaa) as ?orgAA) ?annot_type (str(?txt) as ?note) (str(?varaa) as ?varAA) ?freq where
{
 values ?entry {nextprot:NX_Q9BYF1 nextprot:NX_O15393} # proteins of interest (ACE2, TMPRSS2)
 ?entry :gene /:recommendedName / rdfs:label ?gn.
 ?entry :isoform ?iso .
 ?iso :swissprotDisplayed true; :variant ?var .
 ?var :start ?pos ; :original ?orgaa; :variation ?varaa .
 optional {?var :evidence / :alleleFrequency ?freq .}
 ?var :evidence / :reference ?xref .
 ?iso :positionalAnnotation ?annot .
 optional {?annot rdfs:comment ?txt .}
 ?annot a ?annot_type .
 {
 ?annot :start ?pos; :end ?pos.
 filter not exists {?annot a :Variant. }
 filter not exists {?annot a :SequenceConflict. }
 }
 union
 {
 ?annot a :DisulfideBond.
 {?annot :start ?pos. }
 union
 {?annot :end ?pos. }
 }
 } order by ?entry ?pos'''
.
