# Select all ChEBI compounds used in Rhea as reaction participant
# 
# This query can not be expressed in the Rhea website

prefix ex: <https://sparql.rhea-db.org/.well-known/sparql-examples/> 
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix schema:<https://schema.org/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#> 
ex:15
    a sh:SPARQLSelectExecutable, sh:SPARQLExecutable ;
    sh:prefixes _:sparql_examples_prefixes ;
    schema:target <https://sparql.rhea-db.org/sparql/> ;
    rdfs:comment """Select all ChEBI compounds used in Rhea as reaction participant"""^^rdf:html ;
    sh:select """# Query 15
# Select all ChEBI compounds used in Rhea as reaction participant
# 
# This query can not be expressed in the Rhea website
SELECT ?chebi ?name (count(?rhea) as ?countRhea)
WHERE {
  ?rhea rdfs:subClassOf rh:Reaction .
  ?rhea rh:side/rh:contains/rh:compound ?compound .
  #
  # the ChEBI can be used either as a small molecule, the reactive part of a macromolecule or as a polymer.
  #
  ?compound (rh:chebi|(rh:reactivePart/rh:chebi)|(rh:underlyingChebi/rh:chebi)) ?chebi .
  ?chebi up:name ?name .
}
GROUP BY ?chebi ?name
ORDER BY DESC(?countRhea)""" .

