prefix ex: <https://sparql.metanetx.org/.well-known/sparql-examples/>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
prefix schema: <https://schema.org/>
ex:6
a sh:SPARQLSelectExecutable, sh:SPARQLExecutable ;
    sh:prefixes _:sparql_examples_prefixes, _:metanetx_sparql_examples_prefixes ;
    schema:target <https://rdf.metanetx.org/sparql/> ;
    rdfs:comment '''Show the reaction equation catalyzed by lactate dehydrogenase (KEGG reaction R00703). NB: Stoichiometric coefficients for substrates are given a negative value.''' ; 
  sh:select '''# Show the reaction equation catalyzed by lactate 
# dehydrogenase (KEGG reaction *R00703*). NB: Stoichiometric 
# coefficients for substrates are given a negative value 

SELECT ?chem ?chem_name ?comp ?comp_name ?coef
WHERE{
    ?reac mnx:reacXref keggR:R00703 .
    ?reac ?side ?part .
    ?part mnx:chem ?chem ;
          mnx:comp ?comp ;
          mnx:coef ?c    .
    ?chem rdfs:comment ?chem_name .
    ?comp rdfs:comment ?comp_name .
    FILTER( ?side IN ( mnx:left , mnx:right ))
    BIND( IF( ?side = mnx:left, - ?c, ?c ) AS ?coef )
}

'''.
