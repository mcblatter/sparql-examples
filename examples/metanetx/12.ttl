prefix ex: <https://sparql.metanetx.org/.well-known/sparql-examples/>
prefix sh: <http://www.w3.org/ns/shacl#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
prefix schema: <https://schema.org/>
ex:12
a sh:SPARQLSelectExecutable, sh:SPARQLExecutable ;
    sh:prefixes _:sparql_examples_prefixes, _:metanetx_sparql_examples_prefixes ;
    schema:target <https://rdf.metanetx.org/sparql/> ;
    rdfs:comment '''Given the protein with UniProt accession number P42588 (PAT_ECOLI, putrescine aminotransferase, EC 2.6.1.82) retrieve all reactions and models in which this polypeptide appears.''' ; 
  sh:select '''# Given the protein with UniProt accession number *P42588* 
# (PAT_ECOLI, putrescine aminotransferase, EC 2.6.1.82) 
# retrieve all reactions and models in which this polypeptide 
# appears. 

SELECT ?mnet_label ?reac_label ?reac_eq ?MNXR (GROUP_CONCAT( ?cata_label; separator=\';\' ) AS ?complex )
WHERE{
    ?pept mnx:peptXref up:P42588 .
    ?cata mnx:pept ?pept ;
          rdfs:label ?cata_label .
    ?gpr mnx:cata ?cata ;
         mnx:reac ?reac .
    ?reac rdfs:label ?reac_label ;
          rdfs:comment ?reac_eq .
    ?mnet mnx:gpr ?gpr ;
          rdfs:label ?mnet_label.
    OPTIONAL{ ?reac mnx:mnxr ?MNXR }
} GROUP BY ?mnet_label ?reac_label ?reac_eq ?MNXR
'''.
