prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
prefix schema:<http://schema.org/>
ex:NXQ_00267
 sh:prefixes _:sparql_examples_prefixes ;
  rdfs:comment '''Proteins binding estradiol and/or similar molecules (similarity search with SMILES), and their associated GO_MF terms''' ;
 schema:keyword "small molecule interaction";
 schema:keyword "federated query";
 schema:keyword "tutorial";
 schema:keyword "similarity";
 schema:keyword "function";
  sh:select '''SELECT distinct ?entry (group_concat(distinct str(?gomflab); SEPARATOR = \",\") as ?gomfx) WHERE {
 		SERVICE <https://idsm.elixir-czech.cz/sparql/endpoint/idsm> {
 		SERVICE <https://idsm.elixir-czech.cz/sparql/endpoint/chembl> {
 		?compound sachem:similarCompoundSearch [ sachem:query \"CC12CCC3C(C1CCC2O)CCC4=C3C=CC(=C4)O\" ] . # smiles chain for estradiol
		 }
		 		?ACTIVITY rdf:type cco:Activity;
		cco:hasMolecule ?compound;
 cco:hasAssay ?ASSAY.
		 ?ASSAY cco:hasTarget ?TARGET.
		 ?TARGET cco:taxonomy <http://identifiers.org/taxonomy/9606> . # human protein target
		 ?TARGET cco:hasTargetComponent ?COMPONENT.
		 ?COMPONENT cco:targetCmptXref ?UNIPROT.
		 filter(contains(str(?UNIPROT),\"uniprot\"))
		 }
	?entry skos:exactMatch ?UNIPROT.
	?entry :isoform ?iso.
	?iso :goMolecularFunction / :term ?gomf .
	?gomf rdfs:label ?gomflab .
} 
group by ?entry'''
.
