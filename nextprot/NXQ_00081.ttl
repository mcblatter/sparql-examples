prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
ex:NXQ_00081
 sh:prefixes _:sparql_examples_prefixes ;
  rdfs:comment '''Proteins with at least one 3D structure and that are located in the mitochondrion and are linked with a disease''' ;
  sh:select '''select distinct ?entry where {
 values ?mitoloc {nextprot_cv:SL-0173 nextprot_cv:GO_0005739} # SL and GO values for mitochondrion
 ?entry np:isoform ?iso.
 ?iso np:pdbMapping ?map.
 ?iso np:cellularComponent ?loc .
 ?loc np:term /np:childOf ?mitoloc . # mitochondrial
 filter not exists { ?loc np:negativeEvidence ?negev. } # No negative localization evidence
 {
 ?iso np:medical / rdf:type np:Disease.
 } union {
 ?iso np:uniprotKeyword / np:term ?kw .
 ?kw np:termType \"Disease\"^^xsd:string
 filter (?kw != nextprot_cv:KW-0656)
 }
}'''
.
