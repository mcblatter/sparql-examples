prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
ex:NXQ_00225
 sh:prefixes _:sparql_examples_prefixes ;
  rdfs:comment '''Proteins with high proline content''' ;
  sh:select '''select distinct ?entry ?AAcnt ?seqlen ?AAdensity where {
 ?entry np:isoform ?iso.
 ?iso np:swissprotDisplayed true .
 ?iso np:sequence / np:chain ?seq .
 ?iso np:sequence / np:length ?seqlen .
 bind( strlen( replace( str(?seq), \"[^P]\", \"\")) as ?AAcnt) # replace all non-Proline by empty string
 bind(xsd:float(?AAcnt)/xsd:float(?seqlen) as ?AAdensity)
 filter(?AAcnt > 10)
}
order by desc(?AAdensity)
limit 100 # will bring-up the 100 most relevant cases'''
.
