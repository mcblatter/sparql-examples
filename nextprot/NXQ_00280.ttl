prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
ex:NXQ_00280
 sh:prefixes _:sparql_examples_prefixes ;
  rdfs:comment '''Proteins interacting with at least 10 members of a protein family ''' ;
  sh:select '''select distinct ?entry (str(?gen) as ?gene) (str(?xlab) as ?itfamily) ?itcnt ?membercnt ((xsd:float(?itcnt) / xsd:float(?membercnt)) as ?ratio) where {
 {
 select ?xterm ?xlab (count(distinct ?member) as ?membercnt) where {
 ?member np:isoform? / np:familyName /np:term /np:childOf ?xterm.
 ?xterm rdfs:label ?xlab .
 } group by ?xterm ?xlab
 } 
 {
 select ?xterm ?entry ?gen (count (distinct ?interactant) as ?itcnt) where {
 ?entry np:isoform ?iso; np:gene / np:name / rdfs:label ?gen .
 ?iso np:interaction ?it.
 ?it np:quality np:GOLD; np:interactant ?interactant.
 ?interactant np:familyName /np:term /np:childOf ?xterm . 
 } group by ?xterm ?entry ?gen having (count (distinct ?interactant) >= 10)	
 }
} 
order by desc(?ratio)
limit 60'''
.
