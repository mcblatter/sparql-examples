prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
prefix schema:<http://schema.org/>
ex:NXQ_00280
 sh:prefixes _:sparql_examples_prefixes ;
  rdfs:comment '''Proteins interacting with at least 10 members of a protein family ''' ;
 schema:keyword "PPI";
 schema:keyword "tutorial";
 schema:keyword "family";
  sh:select '''select distinct ?entry (str(?gen) as ?gene) (str(?xlab) as ?itfamily) ?itcnt ?membercnt ((xsd:float(?itcnt) / xsd:float(?membercnt)) as ?ratio) where {
 {
 select ?xterm ?xlab (count(distinct ?member) as ?membercnt) where {
 ?member :isoform? / :familyName /:term /:childOf ?xterm.
 ?xterm rdfs:label ?xlab .
 } group by ?xterm ?xlab
 } 
 {
 select ?xterm ?entry ?gen (count (distinct ?interactant) as ?itcnt) where {
 ?entry :isoform ?iso; :gene / :name / rdfs:label ?gen .
 ?iso :interaction ?it.
 ?it :quality :GOLD; :interactant ?interactant.
 ?interactant :familyName /:term /:childOf ?xterm . 
 } group by ?xterm ?entry ?gen having (count (distinct ?interactant) >= 10)	
 }
} 
order by desc(?ratio)
limit 60'''
.
