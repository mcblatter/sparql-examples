prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
prefix schema:<http://schema.org/>
ex:NXQ_00246
 sh:prefixes _:sparql_examples_prefixes ;
  rdfs:comment '''Proteins which are enzymes catalyzing a reaction involving lipids''' ;
 schema:keyword "enzyme";
 schema:keyword "federated query";
 schema:keyword "tutorial";
 schema:keyword "function";
  sh:select '''
select distinct ?entry where {
#select distinct ?entry str(?xlab) where {
 service <https://sparql.rhea-db.org/sparql> {
 SELECT distinct ?chebi WHERE {
 ?reaction rdfs:subClassOf rh:Reaction .
 ?reaction rh:status rh:Approved .
 ?reaction rh:side ?reactionSide .
 ?reactionSide rh:contains ?participant .
 ?participant rh:compound ?compound .
 ?compound rh:chebi ?chebi .
 ?chebi rdfs:subClassOf+ CHEBI:18059 .
 } 
 } 
 ?entry :isoform / :smallMoleculeInteraction / :interactant ?x .
 ?x rdfs:label ?xlab .
 ?x skos:exactMatch ?chebi .
} order by ?entry'''
.
