prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
ex:NXQ_00246
 sh:prefixes _:sparql_examples_prefixes ;
  rdfs:comment '''Proteins which are enzymes catalyzing a reaction involving lipids''' ;
  sh:select '''PREFIX rh:<http://rdf.rhea-db.org/>
PREFIX ch:<http://purl.obolibrary.org/obo/>

select distinct ?entry where {
#select distinct ?entry str(?xlab) where {
 service <https://sparql.rhea-db.org/sparql> {
 SELECT distinct ?chebi WHERE {
 ?reaction rdfs:subClassOf rh:Reaction .
 ?reaction rh:status rh:Approved .
 ?reaction rh:side ?reactionSide .
 ?reactionSide rh:contains ?participant .
 ?participant rh:compound ?compound .
 ?compound rh:chebi ?chebi .
 ?chebi rdfs:subClassOf+ ch:CHEBI_18059 .
 } 
 } 
 ?entry np:isoform / np:smallMoleculeInteraction / np:interactant ?x .
 ?x rdfs:label ?xlab .
 ?x skos:exactMatch ?chebi .
} order by ?entry'''
.
