prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
ex:NXQ_00294
 sh:prefixes _:sparql_examples_prefixes ;
  rdfs:comment '''Variants in MECP2 causing Rett Syndrome''' ;
  sh:select '''select distinct ?start ?stop ?original ?modified where { 
 entry:NX_P51608 np:isoform ?iso. #MECP2 entry 
 ?iso np:swissprotDisplayed true. #Swissprot canonical isoform 
 {
 ?iso np:variant ?v.
 ?v np:disease nextprot_cv:DI-00999. #UniProtKB term for Rett Syndrome
 ?v np:start ?start.
 ?v np:end ?stop.
 filter((?stop - ?start) = 0) #single amino acid variants (SAAVs)
 ?v np:original ?original.
 ?v np:variation ?modified.
 }
 union
 {
 ?iso np:proteoform ?pf.
 ?pf np:diseaseRelatedVariant ?ann.
 filter not exists {?ann np:negativeEvidence ?negev} # No negative disease evidence
 ?ann np:impactedObject /np:term nextprot_cv:C75488. #NCI Thesaurus term for Rett Syndrome
 ?pf np:difference ?v.
 ?v np:start ?start.
 ?v np:end ?stop.
 filter((?stop - ?start) = 0)
 ?v np:original ?original.
 ?v np:variation ?modified.
 }
}
order by asc(?start)'''
.
