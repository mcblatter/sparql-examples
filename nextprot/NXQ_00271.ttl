prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
ex:NXQ_00271
 sh:prefixes _:sparql_examples_prefixes ;
  rdfs:comment '''Polymorphisms located on ACE2 and TMPRSS2 and affecting proteinsâ€™ activity, structure, PTM...''' ;
  sh:select '''select distinct (str(?gn) as ?gene) ?pos (str(?snpac) as ?varid) (str(?orgaa) as ?orgAA) ?annot_type (str(?txt) as ?note) (str(?varaa) as ?varAA) ?freq where
{
 values ?entry {entry:NX_Q9BYF1 entry:NX_O15393} # proteins of interest (ACE2, TMPRSS2)
 ?entry np:gene /np:recommendedName / rdfs:label ?gn.
 ?entry np:isoform ?iso .
 ?iso np:swissprotDisplayed true; np:variant ?var .
 ?var np:start ?pos ; np:original ?orgaa; np:variation ?varaa .
 optional {?var np:evidence / np:alleleFrequency ?freq .}
 ?var np:evidence / np:reference ?xref .
 ?iso np:positionalAnnotation ?annot .
 optional {?annot rdfs:comment ?txt .}
 ?annot a ?annot_type .
 {
 ?annot np:start ?pos; np:end ?pos.
 filter not exists {?annot a np:Variant. }
 filter not exists {?annot a np:SequenceConflict. }
 }
 union
 {
 ?annot a np:DisulfideBond.
 {?annot np:start ?pos. }
 union
 {?annot np:end ?pos. }
 }
 } order by ?entry ?pos'''
.
