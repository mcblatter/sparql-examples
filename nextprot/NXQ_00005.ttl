prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
ex:NXQ_00005
 sh:prefixes _:sparql_examples_prefixes ;
  rdfs:comment '''Proteins located in mitochondrion and that lack a transit peptide''' ;
  sh:select '''select distinct ?entry where {
 values ?mitoloc {nextprot_cv:SL-0173 nextprot_cv:GO_0005739 } # SL and GO values for mitochondrion
 ?entry np:isoform ?iso.
 filter not exists { ?iso np:uniprotKeyword /np:term nextprot_cv:KW-0809 } # Transit peptide
 ?iso np:cellularComponent ?loc .
 ?loc np:term /np:childOf ?mitoloc.
 filter not exists {?loc np:negativeEvidence ?negev} # No negative localization evidence
}

# Variables start with ? and can be given any name.
# Variables such as ?entry and ?mitoloc (location in the mitochondrion) are easily understood by humans.'''
.
