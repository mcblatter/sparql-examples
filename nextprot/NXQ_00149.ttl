prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
ex:NXQ_00149
 sh:prefixes _:sparql_examples_prefixes ;
  rdfs:comment '''BRCA1 variants with at least 5 different Severe phenotypes''' ;
  sh:select '''SELECT 
?pfname 
(count(?pvlabel) as ?severe_count) 
(group_concat(?pvlabel;separator=\", and \") as ?severe_list) 
WHERE {
 entry:NX_P38398 np:isoform / np:proteoform ?pf .
 ?pf rdfs:label ?pfname .
 ?pf np:phenotypicVariation ?pv1 .
 ?pv1 rdfs:comment ?pvlabel .
 ?pv1 np:evidence / np:severity np:Severe . 
 }
group by ?pfname having (count(?pvlabel)>=5)
order by desc(count(?pvlabel)) ?pfname'''
.
