prefix ex: <https://sparql.nextprot.org/.well-known/sparql-examples>
prefix sh: <http://www.w3.org/ns/shacl#> 
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:<http://www.w3.org/2000/01/rdf-schema#>
prefix schema:<http://schema.org/>
ex:NXQ_00140
 sh:prefixes _:sparql_examples_prefixes ;
  rdfs:comment '''Proteins that interact with viral proteins''' ;
 schema:keyword "PPI";
 schema:keyword "federated query";
 schema:keyword "tutorial";
  sh:select '''
select distinct ?entry where {
 ?entry :isoform ?iso.
 optional {?iso :interactionInfo ?itinfo .}
 {
 ?itinfo rdfs:comment ?txt.
 filter(contains(?txt,\"viral\") || contains(?txt,\"virus\")).
 } # refers to a virus in the free-text interactionInfo topic
UNION
 { ?iso :uniprotKeyword / :term nextprot_cv:KW-0945.} # host-virus interaction keyword
UNION
 {
 SERVICE <http://sparql.uniprot.org/sparql> {
 SELECT distinct ?viralinteractor WHERE # get viral proteins with an IntAct xref
 {
	?viralinteractor a up:Protein .
	?viralinteractor rdfs:seeAlso ?db .
	?db up:database <http://purl.uniprot.org/database/IntAct> .
 	?viralinteractor up:organism ?tax .
	?tax up:scientificName ?orgname.
	filter(contains(?orgname,\"virus\"))
 }
 }
 ?entry :isoform / :binaryInteraction ?it .
 ?it :interactant ?interactant; :quality :GOLD. # NeXtprot entries with a GOLD IntAct binary interaction
 ?interactant skos:exactMatch ?viralinteractor . # interactant must be in the uniprot service result set to select the entry
 } 
UNION
 {
 SERVICE <http://sparql.uniprot.org/sparql> {
 SELECT distinct ?humprotein WHERE # get human proteins that share a PDB xref with a viral protein (same PDB id)
 {
	?humprotein a up:Protein .
 ?humprotein up:organism taxon:9606 . 
	?humprotein rdfs:seeAlso ?db .
	?db up:database <http://purl.uniprot.org/database/PDB> .
	?viralprotein a up:Protein .
	?viralprotein rdfs:seeAlso ?db .
 ?viralprotein up:organism ?tax .
	?tax up:scientificName ?orgname.
	filter(contains(?orgname,\"virus\"))
 }
 }
 ?entry skos:exactMatch ?humprotein .	
 } 
}
order by ?entry'''
.
